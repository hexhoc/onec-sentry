#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

Функция НастройкиСервиса() Экспорт
	
	Настройки = РегистрыСведений.Sentry_НастройкиПодключенияКСервису.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	
	СтруктураНастроек = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
		Настройки, Метаданные.РегистрыСведений.Sentry_НастройкиПодключенияКСервису);
	
	Возврат СтруктураНастроек;

КонецФункции

// Возвращает структуру, содержащую значения общих настроек.
// Если в параметр ИменаНастроек передана пустая строка, то возвращается структура со всеми полями регистра.
//
// Параметры:
//  ИменаНастроек - Строка - имена ресурсов регистра, перечисленные через запятую, в формате
//  	требований к свойствам структуры. 
//                      
// Возвращаемое значение:
//	Структура из КлючИЗначение:
//		* Ключ - Строка - имя настройки (поля в регистре).
//		* Значение - Произвольный - значение настройки.
//
Функция ЗначенияНастроек(ИменаНастроек = "") Экспорт
	
	Результат = Новый Структура;
	ТекстЗапросаПолей = "";
	ВыбираемыеПоля = Новый Массив;
	РесурсыРегистра = Метаданные.РегистрыСведений.Sentry_НастройкиПодключенияКСервису.Ресурсы;
	
	// Определяем набор выбираемых полей из регистра
	Если Не ПустаяСтрока(ИменаНастроек) Тогда
		
		// Выбираем те из переданных имен, которые соответствуют метаданным
		МассивИмен = СтрРазделить(СтрЗаменить(ИменаНастроек, " ", ""), ",", Ложь);
		
		Для Каждого ИмяНастройки Из МассивИмен Цикл
			Если РесурсыРегистра.Найти(ИмяНастройки) <> Неопределено Тогда
				ВыбираемыеПоля.Добавить(ИмяНастройки);	
			КонецЕсли;
		КонецЦикла;
	Иначе
		
		// Будем выбирать все поля
		Для Каждого Ресурс Из РесурсыРегистра Цикл
			ВыбираемыеПоля.Добавить(Ресурс.Имя);	
		КонецЦикла;
	КонецЕсли;
	
	Если ВыбираемыеПоля.Количество() = 0 Тогда
		Возврат Результат;	
	КонецЕсли;
	
	// Формирование текста запроса к выбираемым полям	
	Для Каждого ИмяПоля Из ВыбираемыеПоля Цикл
					
		ТекстЗапросаПолей = ТекстЗапросаПолей + ?(ПустаяСтрока(ТекстЗапросаПолей), "", ",") + "
			|	" + ИмяПоля + " КАК " + ИмяПоля;
		
		// Предварительное добавление поля в возвращаемый результат
		Результат.Вставить(ИмяПоля);
		
	КонецЦикла;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	   "ВЫБРАТЬ
       |	&ТекстЗапросаПолей
       |ИЗ
       |	РегистрСведений.Sentry_НастройкиПодключенияКСервису КАК Sentry_НастройкиПодключенияКСервису";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаПолей", ТекстЗапросаПолей);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Изменяет в регистре значения настроек.
// Параметры:
//	СтруктураНастроек - Структура из КлючИЗначение:
//		* Ключ - Строка - имя ресурса в регистре.
//		* Значение - Произвольный - новое значение ресурса.
//
Процедура ИзменитьНастройки(СтруктураНастроек) Экспорт
	
	МЗ = РегистрыСведений.Sentry_НастройкиПодключенияКСервису.СоздатьМенеджерЗаписи();
	МЗ.Прочитать();
	ЗаполнитьЗначенияСвойств(МЗ, СтруктураНастроек);
	МЗ.Записать();
		
КонецПроцедуры

// Возвращает значение настройки по ее имени.
//	
// Параметры:
//	ИмяНастройки - Строка - имя ресурса в регистре.
//
// Возвращаемое значение:
//	Произвольный - значение ресурса из регистра. Если настройки отсутствуют, возвращает значение по умолчанию на основании типа данных ресурса.
//
Функция ЗначениеНастройки(ИмяНастройки) Экспорт
	
	Настройка = Метаданные.РегистрыСведений.Sentry_НастройкиПодключенияКСервису.Ресурсы.Найти(ИмяНастройки);
	ЗначениеПоУмолчанию = ?(Настройка = Неопределено, Неопределено, Настройка.Тип.ПривестиЗначение(Неопределено));
	
	Настройки = ЗначенияНастроек(ИмяНастройки);
	Возврат ?(Настройки.Свойство(ИмяНастройки) И Настройки[ИмяНастройки] <> Неопределено, Настройки[ИмяНастройки], ЗначениеПоУмолчанию);
		
КонецФункции

// Проверяет, есть ли в регистре настройки подключения.
// Возвращает Истина, если в регистре есть настройка, и Ложь - иначе.
//
// Возвращаемое значение:
// 	Булево
//
Функция ЕстьНастройкиПодключения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый  Запрос;
	Запрос.Текст = 
	   "ВЫБРАТЬ
	   |	Sentry_НастройкиПодключенияКСервису.АдресСервиса КАК АдресСервиса
	   |ИЗ
	   |	РегистрСведений.Sentry_НастройкиПодключенияКСервису КАК Sentry_НастройкиПодключенияКСервису";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();

КонецФункции

#КонецОбласти

#КонецЕсли