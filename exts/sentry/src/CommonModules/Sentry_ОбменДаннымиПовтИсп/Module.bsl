
#Область СлужебныеПроцедурыИФункции

Функция ВерсияПриложения() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ВерсияПриложения;
	
КонецФункции

// Получить представление физического места размещения информационной базы для отображения администратору.
//
// Возвращаемое значение:
//	Строка - представление информационной базы.
//
// Пример:
// - для ИБ в файлом режиме: \\FileServer\1c_ib\
// - для ИБ в серверном режиме: ServerName:1111 / information_base_name.
//
Функция ПредставлениеИнформационнойБазы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтрокаСоединенияСБД = СтрокаСоединенияСБД();
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая(СтрокаСоединенияСБД) Тогда
		Возврат Сред(СтрокаСоединенияСБД, 6, СтрДлина(СтрокаСоединенияСБД) - 6);
	КонецЕсли;
		
	// Прибавить к имени сервера имя пути информационной базы.
	ПозицияПоиска = СтрНайти(ВРег(СтрокаСоединенияСБД), "SRVR=");
	Если ПозицияПоиска <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПозицияТочкиСЗапятой = СтрНайти(СтрокаСоединенияСБД, ";");
	НачальнаяПозицияКопирования = 6 + 1;
	КонечнаяПозицияКопирования = ПозицияТочкиСЗапятой - 2; 
	
	ИмяСервера = Сред(СтрокаСоединенияСБД, НачальнаяПозицияКопирования, КонечнаяПозицияКопирования - НачальнаяПозицияКопирования + 1);
	
	СтрокаСоединенияСБД = Сред(СтрокаСоединенияСБД, ПозицияТочкиСЗапятой + 1);
	
	// Позиция имени сервера
	ПозицияПоиска = СтрНайти(ВРег(СтрокаСоединенияСБД), "REF=");
	Если ПозицияПоиска <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НачальнаяПозицияКопирования = 6;
	ПозицияТочкиСЗапятой = СтрНайти(СтрокаСоединенияСБД, ";");
	КонечнаяПозицияКопирования = ПозицияТочкиСЗапятой - 2; 
	
	ИмяИБНаСервере = Сред(СтрокаСоединенияСБД, НачальнаяПозицияКопирования, КонечнаяПозицияКопирования - НачальнаяПозицияКопирования + 1);
	ПутьКБД = ИмяСервера + "/ " + ИмяИБНаСервере;
	
	Структура = Новый Структура("ИмяСервера, ИмяИБНаСервере, ПутьКБД", ИмяСервера, ИмяИБНаСервере, ПутьКБД);	
	
	Возврат Структура;
	
КонецФункции

Функция СтрокаСоединенияСБД() Экспорт
	// Убираем дефолтный порт. Строка соединения может быть с ним и без него для одной и той же базы. 
	Возврат СтрЗаменить(СтрокаСоединенияИнформационнойБазы(), ":1541", "");
КонецФункции

Функция ПерекодироватьТекст(КодируемыйТекст, НачальнаяКодировка = "utf-8", КонечнаяКодировка = "windows-1251", Отказ = Ложь) Экспорт
	
	ПерекодируемыйТекст = "";
	
	Попытка
		
		Стрим			= Новый COMОбъект("Adodb.Stream");
		Стрим.Type		= 2;
		Стрим.Mode		= 3;
		Стрим.charset	= НачальнаяКодировка;
		
		Стрим.Open();
		
		Стрим.WriteText(КодируемыйТекст);
		Стрим.Position		= 0;
		Стрим.charset		= КонечнаяКодировка;
		ПерекодируемыйТекст	= Стрим.ReadText(-1);		
		
		Стрим.Close();
		
	Исключение
		
		Отказ = Истина;
		ПерекодируемыйТекст = КодируемыйТекст;
		
	КонецПопытки;
	
	Возврат ПерекодируемыйТекст;	

КонецФункции


#КонецОбласти

