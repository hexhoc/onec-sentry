
#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьНастройкиПриИзмененииПредставленияИнформационнойБазы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка

		// Удалим настройки подключения к сервису
		НЗ = РегистрыСведений.Sentry_НастройкиПодключенияКСервису.СоздатьНаборЗаписей();
		НЗ.Записать();	
		
		// Удалим регламент обмена в сервисом Sentry	
		Sentry_РегламентноеЗаданиеСервер.УдалитьРегламентныеЗадания();
		
		ОбновитьПовторноИспользуемыеЗначения();
		
		ТекстОшибки = НСтр("ru='Пользователь подтвердил, что это копия информационной базы. 
			|Настройки подключения очищены. Регламентное задание обмена с сервисом Sentry удалено. Токен интеграции удален'");
		ЗаписьЖурналаРегистрации(НСтр("ru='Sentry.ОчиститьНастройкиПриИзмененииПредставленияИнформационнойБазы'"), 
			УровеньЖурналаРегистрации.Информация, , Sentry_РегламентноеЗаданиеСервер.НаименованиеРегламентногоЗаданияSentry(), ТекстОшибки);
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = СтрШаблон(НСтр("ru='Пользователь подтвердил, что это копия информационной базы. 
			|Не удалось удалить следующие настройки: Настройки подключения, Регламентное задание обмена с сервисом Sentry, Токен интеграции. %1'"),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(НСтр("ru='Sentry.ОчиститьНастройкиПриИзмененииПредставленияИнформационнойБазы'"), 
			УровеньЖурналаРегистрации.Ошибка, , Sentry_РегламентноеЗаданиеСервер.НаименованиеРегламентногоЗаданияSentry(), ТекстОшибки);
			
	КонецПопытки;
		
КонецПроцедуры

Процедура ОбновитьПредставлениеИнформационнойБазыПриИзмененииНастроек() Экспорт
	
	Попытка
		
		СтрокаСоединенияИнформационнойБазы = Sentry_ОбменДаннымиПовтИсп.СтрокаСоединенияСБД();
		
		ОбщиеНастройки = РегистрыСведений.Sentry_НастройкиПодключенияКСервису.ЗначенияНастроек();
		ОбщиеНастройки.СохраненныйПутьДоИнформационнойБазы = СтрокаСоединенияИнформационнойБазы;
		РегистрыСведений.Sentry_НастройкиПодключенияКСервису.ИзменитьНастройки(ОбщиеНастройки);
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Sentry.ОбновитьПредставлениеИнформационнойБазыПриИзмененииНастроек'"), УровеньЖурналаРегистрации.Информация, , 
			Sentry_РегламентноеЗаданиеСервер.НаименованиеРегламентногоЗаданияSentry(), НСтр("ru='Записан новый путь до информационной базы'"));
		
	Исключение
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Sentry.ОбновитьПредставлениеИнформационнойБазыПриИзмененииНастроек'"), УровеньЖурналаРегистрации.Ошибка, , 
			Sentry_РегламентноеЗаданиеСервер.НаименованиеРегламентногоЗаданияSentry(), НСтр("ru='Не удалось записать новый путь до информационной базы'"));
	 
	КонецПопытки;	
		
КонецПроцедуры

Функция СохраненныйПутьДоИнформационнойБазы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегистрыСведений.Sentry_НастройкиПодключенияКСервису.ЗначениеНастройки("СохраненныйПутьДоИнформационнойБазы");
		
КонецФункции



#КонецОбласти