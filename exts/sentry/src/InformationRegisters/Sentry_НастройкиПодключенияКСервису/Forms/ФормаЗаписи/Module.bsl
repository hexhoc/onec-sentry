
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗначенияНастроек = РегистрыСведений.Sentry_НастройкиПодключенияКСервису.ЗначенияНастроек();
	ЗаполнитьЗначенияСвойств(Запись, ЗначенияНастроек);                                        
	Если НЕ ЗначениеЗаполнено(Запись.СохраненныйПутьДоИнформационнойБазы) Тогда
		Запись.СохраненныйПутьДоИнформационнойБазы = Sentry_ОбменДаннымиПовтИсп.СтрокаСоединенияСБД();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.АдресСервиса) Тогда
		Протокол = ?(Запись.ЗащищенныйИнтернетПротокол, "https", "http");
		SentryDSN = Протокол + "://" + Запись.Ключ + "@" + Запись.АдресСервиса + "/" + Запись.ПроектID;
	КонецЕсли;
	
	Элементы.ДекорацияРасшифровкаУровня.Заголовок = ПолучитьПодсказкуПоУровнюЛогирования(Запись.УровеньЛогирования);

КонецПроцедуры  


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УровеньЛогированияПриИзменении(Элемент)

	Элементы.ДекорацияРасшифровкаУровня.Заголовок = ПолучитьПодсказкуПоУровнюЛогирования(Запись.УровеньЛогирования);

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Если ПроверитьЗаполнение() Тогда
		Отказ = Ложь;
		ЗаписатьИзмененияСервер(Отказ);
		Если Не Отказ Тогда
			Модифицированность = Ложь;
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.Sentry_НастройкиПодключенияКСервису"));
			Закрыть();
		КонецЕсли;		

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияСервер(Отказ)

	СтруктураИзменений = Новый Структура;
		
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.Sentry_НастройкиПодключенияКСервису");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ТекущиеЗначенияНастроек = РегистрыСведений.Sentry_НастройкиПодключенияКСервису.ЗначенияНастроек();
		
		ЗаполнитьЗначенияСвойств(ТекущиеЗначенияНастроек, Запись);
				
		ПроверитьПодключениеНаСервере(Отказ);
		Если Не Отказ Тогда
			ТекущиеЗначенияНастроек.РегламентноеЗаданиеGUID = СоздатьРегламентноеЗаданиеНаСервере(Запись.ИнтервалВыгрузки);
		КонецЕсли;
		
		РегистрыСведений.Sentry_НастройкиПодключенияКСервису.ИзменитьНастройки(ТекущиеЗначенияНастроек);	

		ЗафиксироватьТранзакцию();
		
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		
		ОтменитьТранзакцию();
		Отказ = Истина;
		
	КонецПопытки;
	
	Если Не Отказ Тогда
		Модифицированность = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		Отказ = Ложь;
		ПроверитьПодключениеНаСервере(Отказ);
		
		Если Не Отказ Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Первый запуск состоялся успешно'")); 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПодключениеНаСервере(Отказ)
	
	// Разрешаем запуск для всех пользователей с правами на подсистему.
	// Иначе нет прав на получение свойств расширения для передачи в заголовки.
	УстановитьПривилегированныйРежим(Истина);
	
	КодОтвета = 0;
	ТекстОшибки = "";
	
	Sentry_ОбменДаннымиСервер.ПервыйЗапуск(Запись.ЗащищенныйИнтернетПротокол, Запись.АдресСервиса, Запись.Порт, Запись.Ключ, Запись.ПроектID, Отказ, ТекстОшибки, КодОтвета);
	
	Если Отказ Тогда		
		ВызватьИсключение ТекстОшибки; 		
	КонецЕсли;
			
	УстановитьПривилегированныйРежим(Ложь);
	
	ПодключениеПроизведено = Не Отказ;
	
	Если ПодключениеПроизведено Тогда		
		ТекстСообщения = НСтр("ru = 'Подключение было успешно установлено.'");
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СоздатьРегламентноеЗаданиеНаСервере(ИнтервалВыгрузкиСек)
	
	// Удаляем если уже существует
	Sentry_РегламентноеЗаданиеСервер.УдалитьРегламентныеЗадания();
	
	// Создаем новое регламентное задание		
	Расписание = Новый РасписаниеРегламентногоЗадания();
	Расписание.ПериодПовтораДней = 1;
	Расписание.ПериодПовтораВТечениеДня = ИнтервалВыгрузкиСек;
	
	РегламентноеЗадание = Sentry_РегламентноеЗаданиеСервер.СоздатьРегламентноеЗадание(Неопределено, Расписание);
	Если РегламентноеЗадание <> Неопределено Тогда
		Возврат РегламентноеЗадание.УникальныйИдентификатор;
	КонецЕсли;

	ТекстСообщения = НСтр("ru = 'Не удалось создать регламентное задание. Проверьте, включены ли в базе регламентные задания и выполните подключение снова.'");
	ВызватьИсключение ТекстСообщения;

КонецФункции

&НаКлиенте
Процедура ЗаполнитьНастройки(Команда)
	
	СтрокаОбработанная= СтрЗаменить(SentryDSN, "://", Символы.ПС);
	СтрокаОбработанная = СтрЗаменить(СтрокаОбработанная, "@", Символы.ПС);
	СтрокаОбработанная = СтрЗаменить(СтрокаОбработанная, "/", Символы.ПС);
	
	// Разделяем строку по "://" для получения протокола
	ЧастиСтроки = СтрРазделить(СтрокаОбработанная, Символы.ПС, Ложь);
	
	Для Индекс = 0 По ЧастиСтроки.Количество() - 1 Цикл
		Если Индекс = 0 Тогда
			Запись.ЗащищенныйИнтернетПротокол = (ЧастиСтроки[Индекс] = "https");
			Если Запись.ЗащищенныйИнтернетПротокол Тогда
				Запись.Порт = 443;
			Иначе
				Запись.Порт = 80;
			КонецЕсли;
		ИначеЕсли Индекс = 1 Тогда
			Запись.Ключ = ЧастиСтроки[Индекс];
		ИначеЕсли Индекс = 2 Тогда
			Запись.АдресСервиса = ЧастиСтроки[Индекс];
		ИначеЕсли Индекс = 3 Тогда
			Запись.ПроектID = ЧастиСтроки[Индекс];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьПодсказкуПоУровнюЛогирования(УровеньЛогирования)
	
	ТекстПодсказки = "Логируем события:";
	
	МассивУровниЛогирования = Новый Массив;
	МассивУровниЛогирования.Добавить("Примечания");
	МассивУровниЛогирования.Добавить("Информация");
	МассивУровниЛогирования.Добавить("Предупреждения");
	МассивУровниЛогирования.Добавить("Ошибки");
	
	УровеньЧисло = 0;
	Если УровеньЛогирования = "Информация" Тогда
		УровеньЧисло = 1;
	ИначеЕсли УровеньЛогирования = "Предупреждение" Тогда
		УровеньЧисло = 2;
	ИначеЕсли УровеньЛогирования = "Ошибка" Тогда
		УровеньЧисло = 3;
	КонецЕсли; 
	
	Для Индекс = УровеньЧисло По МассивУровниЛогирования.Количество() - 1 Цикл
		ТекстПодсказки = 
			ТекстПодсказки + 
			?(ПустаяСтрока(ТекстПодсказки), "", Символы.ПС) + 
			" - " + МассивУровниЛогирования[Индекс];
	КонецЦикла;
	
	Возврат Новый ФорматированнаяСтрока(ТекстПодсказки,,WebЦвета.Серый,,);
	
КонецФункции



#КонецОбласти




